import org.example.GenerateStartScript

plugins {
    id("java")
}

def generateStartScript = tasks.register("generateStartScript", GenerateStartScript) {
    mainClass = "org.example.app.App"
    scriptFile = layout.buildDirectory.file("tmp/scripts/run")
}

def installSpec = copySpec {
    from(generateStartScript)

    into("app") {
        from(tasks.named("jar"))
    }
    into("libs") {
        from(configurations.runtimeClasspath) {
            // filter, include, exclude, ...
            rename {
                it.substring(0, it.lastIndexOf("-")) + ".jar"
            }
        }
        // from(configurations.runtimeClasspath.elements.map { it.collect { jar -> zipTree(jar) } }) {
        //     exclude("META-INF/**")
        // }
    }
}

def packageSpec = copySpec {
    with(installSpec)

    into("meta") {
        from(layout.projectDirectory.file("version.txt"))
    }
}

tasks.register("install", Sync) { // in special cases: <Copy> as alternative
    with(installSpec)
    destinationDir = layout.buildDirectory.dir("install").get().asFile
}

tasks.register("package", Zip) {
    with(packageSpec)
    destinationDirectory = layout.buildDirectory.dir("dist")
}

tasks.named("build") {
    dependsOn(tasks.named("install"))
    dependsOn(tasks.named("package"))
}
