plugins {
    id("java")
    id("jacoco")
}

group = "org.example.my-app"
version = "1.0"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

testing.suites.register("integrationTest", JvmTestSuite) {
    configurations[sources.implementationConfigurationName].extendsFrom(
        configurations.implementation
    )
}

jacoco {
    // ...
}

tasks.named("test") {
    useJUnitPlatform { // JUnit5
        excludeTags("slow")
    }
    reports {
        // ...
    }
}

def testSlow = tasks.register("testSlow", Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags("slow")
    }
}

// Add results of additional test task to 'testResultsElements' to be picked up in report aggregation
configurations.testResultsElementsForTest {
    outgoing.artifact(testSlow.map { it.binaryResultsDirectory })
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

// Centralized versions
dependencies {
    testImplementation(platform("org.junit:junit-bom:6.0.1"))
}
dependencies.constraints {
    implementation("org.apache.commons:commons-lang3:3.20.0")
}
