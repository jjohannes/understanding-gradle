import java.nio.file.Files
import org.gradle.api.plugins.jvm.internal.JvmLanguageUtilities

plugins {
    id("java")
    id("groovy")
}

group = "org.example.my-app"
version = "1.0"

sourceSets.main {
    // java.setSrcDirs(listOf(project.layout.projectDirectory.dir("sources")))
    // resources.setSrcDirs(listOf(project.layout.projectDirectory.dir("resources")))
}

sourceSets.test {

}

def extraFeature = sourceSets.create("extraFeature")
tasks.register(extraFeature.jarTaskName, Jar) {
    from(extraFeature.output)
    archiveClassifier.set("extra-feature")
}

def jvm = services.get(JvmLanguageUtilities)
jvm.registerJvmLanguageSourceDirectory(sourceSets.main, "java17") {
    it.compiledWithJava {
        javaCompiler.set(javaToolchains.compilerFor {
            languageVersion = JavaLanguageVersion.of(17)
        })
        classpath = sourceSets.main.compileClasspath
        doLast {
            def destRoot = destinationDirectory.get().asFile
            def destVersions = new File(destRoot, "META-INF/versions/17")
            destVersions.deleteDir()
            destVersions.mkdirs()
            destRoot.listFiles()?.each {
                if (it.name != "META-INF") {
                    Files.move(it.toPath(), new File(destVersions, it.name).toPath())
                }
            }
        }
    }
}

tasks.jar {
    manifest {
        attributes("Multi-Release": true)
    }
}

dependencies.constraints {
    implementation("org.apache.commons:commons-lang3:3.20.0")
    add("extraFeatureImplementation", "org.apache.commons:commons-lang3:3.20.0")
}

dependencies {
    testImplementation("junit:junit:4.13.2")
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

