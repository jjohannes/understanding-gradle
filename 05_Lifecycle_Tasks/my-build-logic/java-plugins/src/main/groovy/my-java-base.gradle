plugins {
    id("java")
    id("checkstyle")
}

def myBuildGroup = "my project build"
tasks.named("tasks") {
    displayGroup = myBuildGroup
}

tasks.register("tasksAll", TaskReportTask) {
    group = myBuildGroup
    description = "Show additional tasks."
    setShowDetail(true)
}

tasks.named("build") {
    group = myBuildGroup
}
tasks.named("check") {
    group = myBuildGroup
    description = "Runs checks (including tests)."
}

tasks.register("qualityCheck") {
    group = myBuildGroup
    description = "Runs checks (excluding tests)."
    dependsOn(tasks.classes, tasks.checkstyleMain)
    dependsOn(tasks.testClasses, tasks.checkstyleTest)
}

// Hide lifecycle tasks from base plugin we have no use for
tasks.buildDependents { group = "" }
tasks.buildNeeded { group = "" }

tasks.register("lifecycleTasks") {
    group = "help"
    description = "Displays the Lifecycle Tasks of project ':${project.name}'"
    doLast {
        println()
        println("------------------------------------------------------------")
        println("Lifecycle Tasks runnable from project ':${project.name}'")
        println("------------------------------------------------------------")
        println()
        tasks.filter { it.actions.isEmpty() && !it.group.isNullOrEmpty() }.forEach {
            println("${it.name} - ${it.description}")
        }
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:6.0.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}
